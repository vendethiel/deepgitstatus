#!/usr/bin/env raku
subset Dir where $_.IO ~~ :d;
subset GitDir of Dir where "$_/.git".IO ~~ :d;

my $watch-untracked;
sub MAIN(Dir $dir = $*CWD, Bool :$untracked = False) {
  $watch-untracked = $untracked;
  git-list($dir.IO)
}

multi git-list(GitDir $dir) {
  temp $*CWD = $dir;
  my @text = gather given qx`git status` {
    take ' (unpushed commits)'
      if /'Your branch is ahead of'/;
    take ' (changes to be committed)'
      if /'Changes to be committed'/ || /'Changes not staged for commit'/;
    take ' (untracked files present)'
      if $watch-untracked && /:i 'untracked files'/;
  }
  @text.push: ' (no remotes)' if !qx`git remote -v`.trim || qx`git branch -a` !~~ /<|b>'remotes/'/;
  say ($dir.Str, @text).join("\n") if @text;
}

multi git-list(Dir $dir) {
  eager $dir.dir.grep(Dir).map(&git-list);
}
